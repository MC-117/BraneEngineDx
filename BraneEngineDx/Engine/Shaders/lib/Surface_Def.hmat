#include "Basic_FS_Lib.hmat"
#include "ScreenPass_Def.hmat"
#include "CameraData_Def.hmat"

#if defined(VSM_SHADER_FEATURE)
#include "VirtualShadowMap_Def.hmat"
#endif

#define REFLECTION_MASK 1


struct Surface
{
    float4 albedo;
    float3 WorldPos;
    float depth;
    float3 Normal;
    float roughness;
    float2 TexCoords;
    float metallic;
    float ao;
    float4 radiosity;
    float specular;
    uint matID;
    uint mask;
};

void outputSurface(in Surface surf, out float4 A, out float B, out float4 C, out float4 D, out int4 E, out float4 F)
{
    A = surf.albedo;
    B = surf.depth;
    C.xyz = encodeNormal(surf.Normal);
    C.a = 1;
    D.r = surf.roughness;
    D.g = surf.metallic;
    D.b = surf.specular;
    D.a = surf.ao;
    E.rgb = packUIntToUInt3(surf.matID);
    E.a = surf.mask;
    F = surf.radiosity;
}

#if defined(LIGHTING_SHADER_FEATURE)
#define SURFACE_MAIN DEFAULT_SCREEN_MAIN

Surface getSurface(in ScreenVertexOut fin)
{
    Surface surf;
    uint4 E = LOAD_TEX(gBufferE, int3(fin.UV * camData.viewSize, 0));
    uint matID = unpackUInt3ToUInt(E);
    if (matID != materialID)
        discard;
    float4 A = SAMPLE_TEX(gBufferA, fin.UV);
    float B = SAMPLE_TEX(gBufferB, fin.UV);
    float4 C = SAMPLE_TEX(gBufferC, fin.UV);
    float4 D = SAMPLE_TEX(gBufferD, fin.UV);
    float4 F = SAMPLE_TEX(gBufferF, fin.UV);
    surf.albedo = A;
    surf.depth = B;
    surf.WorldPos = uvzPosToWorld(float3(fin.UV, surf.depth));
    surf.TexCoords = fin.UV;
    surf.Normal = decodeNormal(C.xyz);
    surf.roughness = D.r;
    surf.metallic = D.g;
    surf.specular = D.b;
    surf.ao = D.a;
    surf.matID = matID;
    surf.mask = E.a;
    surf.radiosity = F;
    return surf;
}
#elif defined(DEPTH_SHADER_FEATURE) && defined(VSM_SHADER_FEATURE)
#define SURFACE_MAIN DEFAULT_VSM_MAIN

Surface getSurface(in DefaultVSMVertexOut fin)
{
    Surface surf;
    surf.albedo = float4(1, 1, 1, 1);
    surf.WorldPos = float3(0, 0, 0);
    surf.TexCoords = fin.TexCoords;
    surf.depth = fin.clipPos.z;
    surf.Normal = float3(0, 0, 0);
    surf.roughness = 0.5f;
    surf.metallic = 0.5f;
    surf.specular = 1.0f;
    surf.ao = 1.0f;
    surf.matID = -1;
    surf.mask = 0;
    surf.radiosity = float4(0, 0, 0, 0);
    return surf;
}
#else
#define SURFACE_MAIN DEFAULT_FS_MAIN

Surface getSurface(in DefaultVertexOut fin)
{
    Surface surf;
    surf.albedo = float4(1, 1, 1, 1);
    surf.WorldPos = fin.WorldPos;
    surf.TexCoords = fin.TexCoords;
    surf.depth = fin.svPos.z;
    surf.roughness = 0.5f;
    surf.Normal = fin.Normal;
    surf.metallic = 0.5f;
    surf.specular = 1.0f;
    surf.ao = 1.0f;
    surf.matID = materialID;
    surf.mask = 0;
    surf.radiosity = float4(0, 0, 0, 0);
    return surf;
}
#endif

#if defined(DEFERRED_SHADER_FEATURE)

#define FS_DEF_OUT_SURFACE FS_DEF_OUT_GBUFFER

#elif defined(DEPTH_SHADER_FEATURE)

#define FS_DEF_OUT_SURFACE

#else

#define FS_DEF_OUT_SURFACE FS_DEF_OUT_COLOR(FragColor, 0);
//FS_DEF_OUT_COLOR(ssnMask, 0);

#endif

#if defined(DEPTH_SHADER_FEATURE) && defined(VSM_SHADER_FEATURE)
void outputDepth(in DefaultVSMVertexOut fin, float alpha, float clipValue)
{
    clip(alpha - clipValue);
    writeVSMDepth(fin);
}
#else
void outputDepth(in DefaultVertexOut fin, float alpha, float clipValue)
{
    clip(alpha - clipValue);
}
#endif