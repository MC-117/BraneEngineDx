#adapter name pbr_fragment
#fragment
#condition depth
#condition depth.vsm
#condition deferred
#condition lighting
#condition lighting.vsm
#include "../lib/Light_Def.hmat"
#include "../lib/CameraData_Def.hmat"

#include "../lib/PBR_Lib.hmat"
#include "../lib/GBuffer_Def.hmat"

DEF_MAT_BEGIN
uniform float4 baseColor;
uniform float metallic;
uniform float roughness;
uniform float ao;
uniform float normalScale;
uniform float emission;
uniform float clipValue;
DEF_MAT_END

#if defined(DEFERRED_SHADER_FEATURE)
DEF_TEX2D(colorMap, 0);
DEF_TEX2D(metallicMap, 1);
DEF_TEX2D(roughnessMap, 2);
DEF_TEX2D(aoMap, 3);
DEF_TEX2D(normalMap, 4);
DEF_TEX2D(emissionMap, 5);
#elif defined(LIGHTING_SHADER_FEATURE)
DEF_GBUFFER
DEF_TEX2D(depthMap, 6);
#else
DEF_TEX2D(colorMap, 0);
DEF_TEX2D(metallicMap, 1);
DEF_TEX2D(roughnessMap, 2);
DEF_TEX2D(aoMap, 3);
DEF_TEX2D(normalMap, 4);
DEF_TEX2D(emissionMap, 5);
DEF_TEX2D(depthMap, 6);
#endif

#include "../lib/Surface_Def.hmat"

FS_DEF_OUT_BEGIN
FS_DEF_OUT_SURFACE
FS_DEF_OUT_END

#ifndef DEFERRED_SHADER_FEATURE
float getShadow(in DirectLight directLight, in Surface surf)
{
    #if defined(VSM_SHADER_FEATURE)
    return vsmSampleDirectShadow(directLight.vsmID, surf.WorldPos,
        surf.Normal, directLight.shadowBias.y);
    #else
    return ShadowCalculation(depthMap, depthMapSampler,
        surf.WorldPos, surf.Normal, directLight);
    #endif
}

float getShadow(in PointLight pointLight, in Surface surf)
{
    #if defined(VSM_SHADER_FEATURE)
    return 1;
    #else
    return 1;
    #endif
}
#endif

//[earlydepthstencil]
FragmentOut SURFACE_MAIN
{
    FragmentOut fout;

    #ifdef DEPTH_SHADER_FEATURE
    float alpha = SAMPLE_TEX(colorMap, fin.TexCoords).a * baseColor.a;
    outputDepth(fin, alpha, clipValue);
    #else
    Surface surf = getSurface(fin);

    #ifndef LIGHTING_SHADER_FEATURE
    float3x3 TBN = cotangent(surf.Normal, surf.WorldPos, surf.TexCoords);
    float3 tN = normalize(SAMPLE_TEX(normalMap, surf.TexCoords).xyz * 2 - 1) * normalScale;
    surf.Normal = normalize(mul(tN, TBN));
    surf.albedo = baseColor * SAMPLE_TEX(colorMap, surf.TexCoords);
    //surf.albedo.rgb = pow(surf.albedo.rgb, 2.2);
    surf.roughness = roughness * SAMPLE_TEX(roughnessMap, surf.TexCoords).r;
    surf.metallic = metallic * SAMPLE_TEX(metallicMap, surf.TexCoords).r;
    surf.ao = ao * SAMPLE_TEX(aoMap, surf.TexCoords).r;
    surf.radiosity = SAMPLE_TEX(emissionMap, surf.TexCoords);
    surf.radiosity.rgb *= emission;
    surf.mask |= REFLECTION_MASK;
    #endif

    #ifdef DEFERRED_SHADER_FEATURE
    outputSurface(surf, GBUFFER_OUT_PARAMS(fout));
    #else
    float3 V = normalize(camData.camPos - surf.WorldPos);
    // reflectance equation
    float3 Lo = Float3(0.0);

    if (directLight.intensity != 0) {
        float shadowFactor = getShadow(directLight, surf);
        Lo += PBR_Lo_DirectLight(directLight, surf.Normal, V, surf.albedo.xyz,
            surf.metallic, surf.roughness) * shadowFactor;
    }

    for(int i = 0; i < pointLightCount; ++i) {
        if (pointLights[i].intensity != 0) {
            Lo += PBR_Lo_PointLight(pointLights[i], surf.WorldPos, surf.Normal, V, surf.albedo.xyz, surf.metallic, surf.roughness);
        }
    }

    // Combination
    float3 ambient = PBR_Ambient(surf.WorldPos, surf.Normal, surf.albedo.xyz, surf.ao);
    float3 color = ambient + Lo;

    color = PBR_ColorCorrection(color) + (baseColor * surf.radiosity).rgb;

    fout.FragColor = float4(color, surf.albedo.a);
    #endif

    #endif
    return fout;
}