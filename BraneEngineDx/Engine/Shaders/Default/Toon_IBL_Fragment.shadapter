#adapter name toon_ibl_fragment
#fragment
#include "../lib/Light_Def.hmat"
#include "../lib/CameraData_Def.hmat"

DEF_MAT_BEGIN
uniform float4 baseColor;
uniform float4 reflectionColor;
uniform float4 shadowColor;
uniform float4 highlightColor;
uniform float4 rimColor;
uniform float4 overColor;
uniform float shadowCut;
uniform float shadowCutSmooth;
uniform float highlightCut;
uniform float highlightCutSmooth;
uniform float rimCut;
uniform float rimCutSmooth;
uniform float normalScale;
uniform float roughness;
DEF_MAT_END

DEF_TEX2D(colorMap, 0);
DEF_TEX2D(aoMap, 1);
DEF_TEX2D(roughnessMap, 2);
DEF_TEX2D(reflectionMap, 3);
DEF_TEX2D(normalMap, 4);
DEF_TEX2D(depthMap, 5);

#include "../lib/Toon_Lib.hmat"

FS_DEF_OUT_BEGIN
FS_DEF_OUT_COLOR(FragColor, 0);
//FS_DEF_OUT_COLOR(ssnMask, 0);
FS_DEF_OUT_END

FragmentOut DEFAULT_FS_MAIN
{
    FragmentOut fout;
    float3x3 TBN = cotangent(fin.Normal, fin.WorldPos, fin.TexCoords);
    float3 tN = normalize(SAMPLE_TEX(normalMap, fin.TexCoords).xyz * 2 - 1) * normalScale;
    float3 N = normalize(mul(tN, TBN));
    float3 V = normalize(camData.camPos - fin.WorldPos);
    float4 albedo = SAMPLE_TEX(colorMap, fin.TexCoords);
    float ao = SAMPLE_TEX(aoMap, fin.TexCoords).r;
    float a = albedo.a * baseColor.a;
    float3 L = normalize(directLight.direction);
    float3 H = normalize(V + L);
    float4 ambience = lerp(baseColor, highlightColor, smoothToon(Float4(1.0f),
        Float4(0.0f), dot(N, H), highlightCut, highlightCutSmooth));
    ambience = lerp(shadowColor, ambience, ShadowCalculation(depthMap, depthMapSampler, fin.WorldPos, fin.Normal, directLight));
    ambience = smoothToon(ambience, shadowColor, dot(N, L), shadowCut, shadowCutSmooth);
    ambience = smoothToon(rimColor, ambience, fresnelSchlick(dot(N, normalize(camData.camPos - fin.WorldPos)), 0.33f), rimCut, rimCutSmooth);

    float3 R = normalize(-reflect(-V, N));
    float _roughness = SAMPLE_TEX(roughnessMap, fin.TexCoords).r * roughness;
    float2 coord = sphereProj(-R);
    coord.y = 1 - coord.y;
    float4 rel = SAMPLE_TEX_LOD(reflectionMap, coord, _roughness * (getLodLevels(reflectionMap) - 1));

    fout.FragColor = lerp(reflectionColor * rel, albedo, _roughness) * ambience * ao;
    fout.FragColor.a = a;
    fout.FragColor *= overColor;
    //fout.ssnMask = float4(1);
    return fout;
}